/** _pickship v.1 **/
_pickship: procedure expose (globals)
 msg=arg(1)
 if msg="" then msg="choose ships:"
 shiplist=arg(2)
 lra=_lra("get")
 buflen=16*1024
 call zocreceivebuf buflen
 call zocsend "czq"
 if zocwait("<Active Ship Scan>")=640 then call _error "timeout"
 if zocwait("(?=Help)? :")=640 then call _error "timeout"
 buff=translate(zocreceivebuf(0),"  ",d2c(0)||d2c(10))
 if length(buff)=buflen then call _error "buffer overflow"
 i=pos("----------",buff)
 if i>0 then i=pos(d2c(13),buff,i)+1
 if i>1 then j=pos(d2c(13),buff,i)
 c=0
 do while i>0 & j>0
  line=strip(substr(buff,i,j-i))
  if line="" then leave
  c=c+1
  parse var line shipnum shiploc
  if pos("+",shiploc)>0 then do
   plus=pos("+",line)
   current.c=1
   line=left(line,plus-1)||" "||substr(line,plus+1)
  end /*if*/
  else current.c=0
  parse var line shipnum.c shiploc.c line
  shipname.c=left(line,20)
  line=substr(line,21)
  if right(shipname.c,4)="Corp" then shipname.c=left(shipname.c,length(shipname.c)-5)
  parse var line figs.c shields.c hops.c shiptype.c
  shipname.c=strip(shipname.c)
  shiptype.c=strip(shiptype.c)
  if pos("T",figs.c)>0 then figs.c=strip(translate(figs.c," ", "T"))*1000
  if pos("T",shields.c)>0 then shields.c=strip(translate(shields.c," ", "T"))*1000
  if shiplist\="" then do
   found=0
   do k=1 to _dwords(shiplist,";")
    if pos(lower(_dword(shiplist,k,";")),lower(shiptype.c))>0 then found=1
    if found then leave
   end /*do*/
   if \found then c=c-1
  end /*if*/
  i=j+1
  j=pos(d2c(13),buff,i)
 end /*do*/
 menu=""
 do i=1 to c
  menu=menu||";"||copies("  ",3-length(shipnum.i))||shipnum.i
  menu=menu||copies("  ",6-length(shiploc.i))||shiploc.i
  menu=menu||"  "||shiptype.i
  if current.i then menu=menu||" **"
  num=shiploc.i
  if _bust.num>0 then menu=menu||" B"
  if num=lra then menu=menu||" L"
 end /*do*/
 menu=substr(menu,2)
 i=_drequestlisti(msg,menu,";","c")
 rslt=shipnum.i||","||shiploc.i||","||shiptype.i||","||shipname.i
return rslt