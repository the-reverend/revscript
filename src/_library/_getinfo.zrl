/** _getinfo v.6 **/
_getinfo: procedure expose (globals)
 args=lower(arg(1))
 bufflen=32*1024
 
 /* grab a buffer of text containing the quickstats and strip lf/nul */
 call zocreceivebuf bufflen
 call zocsend "/"
 if _wait("Sect ")=640 then call _error "timeout"
 if _wait("Ship ")=640 then call _error "timeout"
 if _wait("^M")=640 then call _error "timeout"
 buff=translate(zocreceivebuf(0),"  ",d2c(10)||d2c(0))
 if length(buff)>=bufflen then call zocwrite "<buffer overflow>"
 
 /* reduce buffer to just the quickstats info and change bars to semicolons */
 i=pos("Sect ",buff)
 j=pos("Ship",buff,i)
 j=pos(d2c(13),buff,j)
 buff=substr(buff,i,j-i)
 buff=_space(translate(buff, ";", d2c(179)),1)
 
 /* strip commas from numbers */
 buff=translate(_space(translate(buff,", "," ,"),0),", "," ,")

 /* remove newlines, strip spaces, and condense to semi-delimited string */
 cr=d2c(13)
 buff2=strip(_dword(buff,1,cr))
 do i=2 to _dwords(buff,cr)
  buff2=buff2||";"||strip(_dword(buff,i,cr))
 end
 buff=buff2
 drop buff2

 /* analyze buffer */
 vr.=""
 vl.=""
 c=0
 do j=1 to _dwords(buff,";")
  c=c+1
  vr.c=strip(_dword(buff,j,";"))
  vl.c=word(vr.c,2)
  vr.c=lower(word(vr.c,1))
  if vr.c="ship" then do
   c=c+1
   vr.c=strip(_dword(buff,j,";"))
   vl.c=word(vr.c,3)
   vr.c="shiptype"
  end /*if*/
 end /*do*/
 c=c+1
 vr.c="all"
 vl.c=buff
 
 /* set global vars */
 _qs.!corp=0
 do i=1 to c
  tag="!"||upper(vr.i)
  _qs.tag=vl.i
 end /*do*/

 /* pass out requested stats */
 rslt=""
 do i=1 to _dwords(args,";")
  found=0
  do j=1 to c
   if vr.j=_dword(args,i,";") then do
    rslt=rslt||";"||vl.j
    found=1
   end /*if*/
  end /*do*/
  if \found then rslt=rslt||";"||"##NA##"
 end /*do*/
 if rslt\="" then rslt=substr(rslt,2)

return rslt
/*- Sect 263|Turns 820|Creds 799,881|Figs 5,000|Shlds 400|Hlds 12|Ore 0|Org 0     -*/
/*- Equ 0|Col 0|Phot 7|Armd 0|Lmpt 0|GTorp 0|TWarp No|Clks 0|Beacns 0|AtmDt 0     -*/
/*- Crbo 0|EPrb 0|MDis 0|PsPrb No|PlScn No|LRS None|Aln -4,496,437|Exp 0|Corp 5   -*/
/*- Ship 102 MisFri                                                               -*/
/*-                                                                               -*/
/*- Sect 4695|Turns 72|Creds 629,278|Figs 999|Shlds 1,250|Hlds 125|Ore 116|Org 0  -*/
/*- Equ 0|Col 0|Phot 0|Armd 91|Lmpt 95|GTorp 10|TWarp 2|Clks 5|Beacns 20|AtmDt 5  -*/
/*- Crbo 0|EPrb 25|MDis 8|PsPrb No|PlScn Yes|LRS Holo|Aln -4,500,000|Exp 39,211   -*/
/*- Corp 5|Ship 63 CorFla                                                         -*/